MODULE TimeEntryAbsense;

REQUIRE TimeEntry, EmployeeAbsence;

absense = DATA Absence (TimeEntry);
descriptionAbsense 'Absence' (TimeEntry t) = CONCAT '-', toDateDDMMYY (dateFrom (absense (t))), toDateDDMMYY (dateTo (absense (t)));

workedHours 'Worked hours' (Absence a) = GROUP SUM hours (TimeEntry t) BY absense (t);

CONSTRAINT workedHours (Absence a)> hours (a) MESSAGE 'The hours worked cannot exceed hours of absence';
CONSTRAINT absense (TimeEntry t) AND employee (t) AND NOT employee (t) == employee (absense (t)) CHECKED BY absense [TimeEntry] MESSAGE 'The employee does not match the absent employee';

FORM dialogAbsense 'Absences'
    OBJECTS t = TimeEntry PANEL
    OBJECTS a = Absence LAST
    PROPERTIES (a) READONLY id, nameEmployee, nameReason, dateFrom, dateTo, hours, workedHours, workingHours, date, note
    ORDERS date (a)
    
    FILTERS employee (t) == employee (a)
;

changeAbsense 'Edit absence' (TimeEntry t) {
    DIALOG dialogAbsense OBJECTS t = t, a INPUT NULL DO {
        absense (t) <- a;
    }
}

EXTEND FORM timeEntry PROPERTIES descriptionAbsense (o) ON CHANGE changeAbsense (o);

DESIGN timeEntry {
    data {
        MOVE PROPERTY (descriptionAbsense (o));
    }
}
