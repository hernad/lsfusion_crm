MODULE ProjectExternal;

REQUIRE Project, Issue, TimeEntry;

NAMESPACE Project;

CLASS External 'External system';
TABLE external (External);


id 'Code' = DATA STRING [15] (External) INDEXED CHARWIDTH 5 IN id;
external (id) = GROUP AGGR External p BY id (p);

name 'Name' = DATA ISTRING [100] (External) CHARWIDTH 20;
url 'Url' = DATA STRING [100] (External) CHARWIDTH 30;
path 'Connection string' = DATA ISTRING [250] (External) CHARWIDTH 100;

FORM external 'External system'
    OBJECTS o = External PANEL
    PROPERTIES (o) id, name, url, path
    
    EDIT External OBJECT o
;

FORM externals 'External systems'
    OBJECTS o = External
    PROPERTIES (o) READONLY id, name, url, path
    PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE
    
    LIST External OBJECT o
;

external = DATA External (Project);
idExternal 'External system code' (Project p) = id (external (p));
nameExternal 'External System' (Project p) = name (external (p));

project = GROUP AGGR Project p BY id (p), external (p);

EXTEND FORM project PROPERTIES nameExternal (o);
EXTEND FORM projects PROPERTIES nameExternal (o);
EXTEND FORM dialogProjects PROPERTIES nameExternal (o);

external = DATA External (Issue);
idExternal 'External system code' (Issue p) = id (external (p));
nameExternal 'External System' (Issue p) = name (external (p));

issue = GROUP AGGR Issue p BY id (p), external (p);

CONSTRAINT (external (project (Issue i)) AND NOT external (project (i)) == external (i))
    OR (project (i) AND NOT external (project (i)) AND external (i)) CHECKED BY project [Issue] MESSAGE 'The external system of the issue does not match the external system of the project';

EXTEND FORM issues PROPERTIES nameExternal (o);
EXTEND FORM dialogIssues PROPERTIES nameExternal (o);

external = DATA External (TimeEntry);
idExternal 'External system code' (TimeEntry p) = id (external (p));
nameExternal 'External System' (TimeEntry p) = name (external (p));

timeEntry = GROUP AGGR TimeEntry p BY id (p), external (p);

EXTEND FORM timeEntries PROPERTIES nameExternal (o);
EXTEND FORM dialogTimeEntries PROPERTIES nameExternal (o);
