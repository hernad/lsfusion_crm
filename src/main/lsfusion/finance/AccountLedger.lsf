MODULE AccountLedger;

REQUIRE LegalEntity;

NAMESPACE Account;

CLASS ABSTRACT AccountLedger 'Account movement';
TABLE accountLedger (AccountLedger);

account = ABSTRACT Account (AccountLedger) MATERIALIZED INDEXED;
date 'Date' = ABSTRACT DATE (AccountLedger) MATERIALIZED;
note 'Note' = ABSTRACT ISTRING [250] (AccountLedger) MATERIALIZED;
sum 'Amount' = ABSTRACT NUMERIC [16,5] (AccountLedger) MATERIALIZED;

balance 'Balance' (Account a) = GROUP SUM sum (AccountLedger l) BY account (l) MATERIALIZED;
balanceCurrency 'Currency balance' (Account a) = round2 (balance (a) * OVERRIDE (rateOn (defaultTypeExchange (), currency (a), currentDate ())), 1);

balance 'Balance' (Account a, DATE d) = balance (a) (-) [GROUP SUM sum (AccountLedger l) IF iterate (date (l), sum (d, 1), currentDate ()) BY account (l )] (a);
balanceAccounts 'Amount of balances' (DATE d) = GROUP SUM balance (Account a, d);

balanceCurrency 'Currency balance' (Account a, DATE d) = balance (a, d) * OVERRIDE rateOn (defaultTypeExchange (), currency (a), d), 1;
balanceAccountsCurrency 'Amount of balances in currency' (DATE d) = GROUP SUM balanceCurrency (Account a, d);

FORM accountBalances 'Account Balances'
    OBJECTS a = Account
    PROPERTIES (a) READONLY nameLegalEntity, number, nameBank, addressBank, MFOBank, note, rate, dateTo, balance, balanceCurrency
    ORDERS nameLegalEntity (a)
    FILTERGROUP active FILTER 'Active' active (a) DEFAULT
    
    OBJECTS l = AccountLedger
    PROPERTIES (l) READONLY date, note, sum
    ORDERS date (l) DESC
    FILTERS account (l) == a, balance (a)
    
    OBJECTS dates = (df = DATE, dt = DATE) PANEL
    PROPERTIES 'Date from' = VALUE (df), 'Date to' = VALUE (dt)
    
    OBJECTS d = DATE
    PROPERTIES vd 'Date' = VALUE (d), balance (a, d), balanceCurrency (a, d)
    ORDERS vd DESC
    FILTERS iterate (d, df, dt)
    
    OBJECTS dd = DATE
    PROPERTIES dd 'Date' = VALUE (dd), balanceAccounts (dd), balanceAccountsCurrency (dd)
    ORDERS dd DESC
    FILTERS iterate (dd, df, dt)
;

DESIGN accountBalances {
    OBJECTS {
        NEW pane {
            type = SPLITV;
            fill = 1;
            MOVE BOX (a);
            NEW tab AFTER BOX (a) {
                type = TABBED;
                fill = 1;
                MOVE BOX (l);
                NEW balances {
                    caption = 'Stock dynamics';
                    MOVE BOX (dates);
                    NEW balance {
                        type = CONTAINERH;
                        fill = 1;
                        MOVE BOX (d) {caption = 'By Count'; }
                        MOVE BOX (dd) {caption = 'For all accounts'; }
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    finance {
        NEW accountBalances;
    }
}
