MODULE Currency;

REQUIRE SystemEvents, Time, MasterData, DefaultData, Finance;

CLASS Currency 'Currency';
TABLE currency (Currency);

name 'Name' = DATA ISTRING [50] (Currency) IN id;
sid 'Currency code' = DATA BPSTRING [3] (Currency);

symbol 'Symbol' = DATA BPSTRING [5] (Currency);
shortName 'Name (abbreviated)' = DATA BPSTRING [3] (Currency);
documentName 'Name (in documents)' = DATA BPSTRING [10] (Currency);
currencyShortName (string) = GROUP AGGR Currency currency WHERE currency IS Currency BY shortName (currency);

nameWord 'Template name declination' = DATA ISTRING [250] (Currency);
nameCoinsWord 'Declination of the exchange unit name for the template' = DATA ISTRING [250] (Currency);

onStarted () + {
    NEWSESSION {
        setNoCancelInTransaction ();
        shortName (Currency c) <- 'BYN' WHERE shortName (c) == 'BLR';
        APPLY;
    }
}

defaultCurrency = DATA Currency ();
nameDefaultCurrency 'Default Currency' = name (defaultCurrency ()) CHARWIDTH 30;

currency (sid) = GROUP AGGR (Currency currency) BY sid (currency);

// Forms

FORM currency 'Currency'
    OBJECTS c = Currency PANEL
    PROPERTIES (c) name, shortName, sid, symbol, documentName, nameWord, nameCoinsWord

    EDIT Currency OBJECT c
;

DESIGN currency {
    OBJECTS {
        NEW word AFTER BOX (c) {
            caption = 'Template caption declension';
            MOVE PROPERTY (nameWord (c)) {caption = 'Currency name (ruble, ruble, rubles)'; }
            MOVE PROPERTY (nameCoinsWord (c)) {caption = 'The name of the exchange unit (kopeck, kopeck, kopeck)'; }
        }
    }
}

FORM currencies 'Currencies'
    OBJECTS c = Currency
    PROPERTIES (c) READONLY name, shortName, sid, symbol, documentName

    PROPERTIES (c) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogCurrencies 'Currencies'
    OBJECTS c = Currency
    PROPERTIES (c) READONLY name, shortName

    LIST Currency OBJECT c
;

EXTEND FORM options
    PROPERTIES () nameDefaultCurrency
;

DESIGN options {
    commons {
        MOVE PROPERTY (nameDefaultCurrency ());
    }
}

// ---------------------------------- Exchange types ------------ ---------------- //

CLASS TypeExchange 'Exchange Type';
TABLE typeExchange (TypeExchange);
TABLE rateExchange (TypeExchange, Currency, DATE);

name 'Name' = DATA ISTRING [50] (TypeExchange) IN id;

currency 'Currency of the exchange type' = DATA Currency (TypeExchange);
nameCurrency 'Currency of exchange type' (TypeExchange typeExchange) = name (currency (typeExchange)) IN base;

FORM typeExchange 'Exchange type'
    OBJECTS t = TypeExchange PANEL
    PROPERTIES (t) name, nameCurrency
    
    EDIT TypeExchange OBJECT t
;

// Calculate rates for the date

rate 'Exchange rate' = DATA NUMERIC [20,13] (TypeExchange, Currency, DATE);
typeExchange (name) = GROUP AGGR TypeExchange typeExchange WHERE typeExchange IS TypeExchange BY name (typeExchange);

rateOn 'Exchange rate' (TypeExchange typeExchange, Currency currency, DATE date) =
    GROUP
          LAST rate (typeExchange, currency, DATE dateIn)
          ORDER dateIn
          WHERE rate (typeExchange, currency, dateIn) AND dateIn <= date COMPLEX;

curRate 'Current exchange rate' (TypeExchange typeExchange, Currency currency) =
    rateOn (typeExchange, currency, currentDate ());

defaultTypeExchange = DATA TypeExchange ();
nameDefaultTypeExchange 'Default exchange type' () = name (defaultTypeExchange ());

EXTEND FORM options PROPERTIES nameDefaultTypeExchange ();

DESIGN options {
    pane {
        commons {
            MOVE PROPERTY (nameDefaultTypeExchange ());
        }
    }
}

// -------------------------------------------- Currency rates - ------------------------------- //

FORM dialogTypeExchangeCurrency 'Exchange rate as of date'
    OBJECTS t = TypeExchange PANEL
    OBJECTS c = Currency PANEL
    OBJECTS d = DATE PANEL
    PROPERTIES READONLY name (t), nameCurrency (t), name (c)
    PROPERTIES val = VALUE (d), rate (t, c, d)

;
DESIGN dialogTypeExchangeCurrency {
    BOX {
        type = CONTAINERV;
        MOVE BOX (t) {
            MOVE PROPERTY (name (t)) {focusable = FALSE; charWidth = 40;}
        }
        MOVE BOX (c) {
            type = CONTAINERV;
            MOVE BOX (d) {
                type = CONTAINERH;

                MOVE PROPERTY (rate (t, c, d));
            };
            NEW row {
                caption = 'Currencies of exchange';
                type = CONTAINERH;
                MOVE PROPERTY (name (c)) {caption = 'Currency (from)'; charWidth = 20;}
                MOVE PROPERTY (nameCurrency (t)) {caption = 'Currency (in)'; charWidth = 20;}
            }
        }
    }
    MOVE TOOLBARBOX;
}

dialog 'Add' (TypeExchange typeExchange, Currency currency) {SHOW dialogTypeExchangeCurrency OBJECTS t = typeExchange, c = currency; } TOOLBAR;
delete 'Delete' (TypeExchange typeExchange, Currency currency, DATE date) {rate (typeExchange, currency, date) <- NULL; } IMAGE 'delete.png';


parent = DATA TypeExchange (TypeExchange);
nameParent 'Parent' = name (parent (TypeExchange t));

isRevers 'Reverse' = DATA BOOLEAN (TypeExchange);

EXTEND FORM typeExchange
    PROPERTIES (t) nameParent, isRevers
;

FORM typeExchangeCurrencyDate 'Currency Rates'

    OBJECTS t = TypeExchange
    PROPERTIES (t) READONLY name, nameCurrency, nameParent, isRevers
    PROPERTIES (t) NEWSESSION NEW, EDIT, DELETE

    OBJECTS c = Currency
    PROPERTIES (c) READONLY name
    PROPERTIES (c) NEWSESSION NEW, EDIT, DELETE
    PROPERTIES (t, c) READONLY curRate

    FILTERGROUP inactive FILTER 'There is a course' curRate (t, c) 'F11' DEFAULT

    OBJECTS d = DATE
    PROPERTIES (d) READONLY VALUE

    PROPERTIES (t, c, d) rate
    PROPERTIES (t, c) dialog DRAW d
    PROPERTIES (t, c, d) delete GRID

    FILTERS rate (t, c, d)

    LIST TypeExchange OBJECT t

;

DESIGN typeExchangeCurrencyDate {
    BOX {
        type = CONTAINERV;
        NEW split {
            fill = 1;
            type = SPLITV;
            MOVE BOX (t);
            NEW topContainer {
                fill = 2;
                type = TABBED;
                NEW currentExchangeRates {
                    caption = 'Current rates';
                    type = SPLITH;
                    MOVE BOX (c);
                    MOVE BOX (d);
                }
                NEW importExchangeRates {
                    caption = 'Import rates';
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    finance {
        NEW FOLDER currencyNavigator 'Currencies and Rates' {
            NEW currencies;
            NEW typeExchangeCurrencyDate;
        }
    }
}
