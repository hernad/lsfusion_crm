MODULE Payment;

REQUIRE LegalEntity, Currency, Finance, AccountLedger;

NAMESPACE Payment;

CLASS Payment 'Payment': AccountLedger;
TABLE payment (Payment);

id 'Code' = DATA STRING [15] (Payment) INDEXED CHARWIDTH 5 IN id;
payment (id) = GROUP AGGR Payment c BY id (c);

legalEntity 'Organization' = DATA LegalEntity (Payment) NONULL;
idLegalEntity 'Organization code' (Payment p) = id (legalEntity (p));
nameLegalEntity 'Organization' (Payment p) = name (legalEntity (p)) IN id;
customer (Payment p) = customer (legalEntity (p)) MATERIALIZED INDEXED;
nameCustomer 'Customer' (Payment p) = name (customer (p));

company = DATA LegalEntity (Payment);
idCompany 'Company code' (Payment p) = id (company (p));
nameCompany 'Company' (Payment p) = name (company (p)) IN id;

CONSTRAINT company (Payment p) AND NOT isCompany (company (p)) CHECKED BY company [Payment] MESSAGE 'The payment company is not a company';

number 'Payment document number' = DATA STRING [15] (Payment) IN id;
date 'Date' = DATA DATE (Payment) IN id;
year 'Year' (Payment p) = extractYear (date (p));
monthNumber 'Month number' (Payment p) = extractMonthNumber (date (p));
sum 'Amount' = DATA NUMERIC [16,5] (Payment);
note 'Note' = DATA ISTRING [100] (Payment);

CLASS PaymentType 'Type of payment';
TABLE paymentType (PaymentType);

name 'Name' = DATA ISTRING [100] (PaymentType) CHARWIDTH 8;

FORM paymentType 'Type of payment'
    OBJECTS o = PaymentType PANEL
    PROPERTIES (o) name
    
    EDIT PaymentType OBJECT o
;

FORM paymentTypes 'Types of payment'
    OBJECTS o = PaymentType
    PROPERTIES (o) READONLY name
    PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE
    
    LIST PaymentType OBJECT o
;

type = DATA PaymentType (Payment);
nameType 'Type of payment' (Payment e) = name (type (e));

currency = DATA Currency (Payment);
nameCurrency 'Payment currency' (Payment p) = name (currency (p));
shortNameCurrency 'Currency (abbreviated)' (Payment p) = shortName (currency (p));
sidCurrency 'Currency (abbreviated)' (Payment p) = sid (currency (p));

sumCurrency 'Amount in currency' (Payment p) = round2 (sum (p) * OVERRIDE rateOn (defaultTypeExchange (), currency (p), date (p)), 1) MATERIALIZED;

sumPayment 'Amount of payments' (Customer c) = GROUP SUM sum (Payment p) BY customer (p) MATERIALIZED;

account = DATA Account (Payment);
numberAccount 'Р / с' (Payment p) = number (account (p)) CHARWIDTH 10;

accountFrom = DATA Account (Payment);
numberAccountFrom 'Р / с с' (Payment p) = number (accountFrom (p)) CHARWIDTH 10;

WHEN LOCAL CHANGED (company (Payment p)) AND NOT CHANGED (account (p)) DO {
    account (p) <- account (company (p));
}

CONSTRAINT account (Payment p) AND NOT company (p) == legalEntity (account (p)) CHECKED BY account [Payment] MESSAGE 'The payment account does not match the company';
CONSTRAINT accountFrom (Payment p) AND NOT company (p) == legalEntity (accountFrom (p)) CHECKED BY accountFrom [Payment] MESSAGE 'The account from the payment does not match the company';

CLASS PaymentAccountLedger: AccountLedger;
paymentAccountLedger = AGGR PaymentAccountLedger WHERE accountFrom (Payment payment);

date (PaymentAccountLedger a) + = date (payment (a));
sum (PaymentAccountLedger a) + = -sum (payment (a));
note (PaymentAccountLedger a) + = note (payment (a));
account (PaymentAccountLedger a) + = accountFrom (payment (a));

date (Payment p) + = date (p);
sum (Payment p) + = sum (p);
note (Payment p) + = note (p);
account (Payment p) + = account (p);

FORM dialogInputSum 'New payment amount'
    OBJECTS n = NUMERIC [16.5] PANEL
    PROPERTIES sum 'Sum' = VALUE (n)
;
DESIGN dialogInputSum {
    size = (300.70);
    BOX (n) {
        fill = 1;
        caption = 'Amount';
        MOVE PROPERTY (sum) {
            fill = 1;
            caption = '';
        }
    }
}

FORM payment 'Payment'
    OBJECTS o = Payment PANEL
    PROPERTIES (o) id, nameLegalEntity, nameCompany, number, date, nameType, sum, shortNameCurrency, sumCurrency, note, numberAccount, numberAccountFrom
        
    EDIT Payment OBJECT o
;

DESIGN payment {
    OBJECTS {
        NEW main FIRST {
            type = CONTAINERH;
            caption = 'Main parameters';
            MOVE PROPERTY (id (o));
            MOVE PROPERTY (date (o));
            MOVE PROPERTY (number (o));
            MOVE PROPERTY (nameType (o));
        }
        NEW contacts AFTER main {
            type = CONTAINERH;
            caption = 'Contacts';
            MOVE PROPERTY (nameLegalEntity (o));
            MOVE PROPERTY (nameCompany (o));
        }
        NEW docs AFTER contacts {
            type = CONTAINERH;
            caption = 'Documents';
        }
        NEW sum AFTER docs {
            type = CONTAINERH;
            caption = 'Amount';
            MOVE PROPERTY (sum (o));
            MOVE PROPERTY (shortNameCurrency (o));
            MOVE PROPERTY (sumCurrency (o));
            MOVE PROPERTY (numberAccount (o));
            MOVE PROPERTY (numberAccountFrom (o));
        }
        NEW note AFTER sum {
            caption = 'Additional options';
            alignment = STRETCH;
            MOVE PROPERTY (note (o));
        }
    }
}

FORM payments 'Payments'
    OBJECTS o = Payment LAST
    PROPERTIES (o) READONLY id, nameLegalEntity, nameCustomer, nameCompany, number, date,
                           year, monthNumber, nameType, sum, shortNameCurrency, sumCurrency, note, numberAccount, numberAccountFrom
    ORDERS date (o), nameLegalEntity (o), nameCompany (o)
    PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE
    ORDERS date (o)
;

FORM dialogPayments 'Payments'
    OBJECTS o = Payment LAST
    PROPERTIES (o) READONLY id, nameLegalEntity, nameCustomer, nameCompany, number, date,
                           year, monthNumber, nameType, sum, shortNameCurrency, sumCurrency, note, numberAccount, numberAccountFrom
    ORDERS date (o), nameLegalEntity (o), nameCompany (o)
    
    LIST Payment OBJECT o
;

NAVIGATOR {
    finance {
        NEW payments;
    }
}
